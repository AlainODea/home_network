#!ipxe
set smartos-build 20130822T211238Z
set boot-url http://example.com/boot-isos

######## MAIN MENU ###################
:start
menu Welcome to iPXE's Boot Menu
item
item --gap -- ------------------------- Operating systems ------------------------------
item smartos    Boot SmartOS (${smartos-build})
item smartos-rescue    Rescue SmartOS (${smartos-build})
item --gap -- ------------------------------ Utilities ---------------------------------
item dban       Darik's Boot and Nuke (DBAN)
item shell      Enter iPXE shell
item reboot     Reboot
item
item exit       Exit (boot local disk)
choose --default smartos --timeout 30000 target && goto ${target}

########## UTILITY ITEMS ####################
:dban
sanboot ${boot-url}/dban-2.2.7_i586.iso

:shell
echo Type exit to get the back to the menu
shell
set menu-timeout 0
goto start

:reboot
reboot

:exit
exit

########## MENU ITEMS #######################
:smartos
kernel /smartos/${smartos-build}/platform/i86pc/kernel/amd64/unix -v -B smartos=true,console=text
initrd /smartos/${smartos-build}/platform/i86pc/amd64/boot_archive
boot

:smartos-rescue
kernel /smartos/${smartos-build}/platform/i86pc/kernel/amd64/unix -v -B console=text,standalone=true,noimport=true,root_shadow='$5$2HOHRnK3$NvLlm.1KQBbB0WjoP7xcIwGnllhzp2HnT.mDO7DpxYA'
initrd /smartos/${smartos-build}/platform/i86pc/amd64/boot_archive
boot

goto start
